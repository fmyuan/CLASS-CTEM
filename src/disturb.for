      SUBROUTINE DISTURB (STEMMASS, ROOTMASS, GLEAFMAS, BLEAFMAS,
     1                       THLIQ,   WILTSM,  FIELDSM,    UWIND,
     2                       VWIND,  LIGHTNG,  FCANCMX, LITRMASS,     
     3                    PRBFRHUC, RMATCTEM, EXTNPROB, PFTAREAB,
     4                         IL1,      IL2,       IG,      ICC,      
     5                         ILG,     SORT, NOL2PFTS,       IC,
     6                    GRCLAREA,    THICE,  
C    7 ------------------ INPUTS ABOVE THIS LINE ----------------------   
     8                    STEMLTDT, ROOTLTDT, GLFLTRDT, BLFLTRDT,
     9                    PFTAREAA, GLCAEMLS, RTCAEMLS, STCAEMLS,
     A                    BLCAEMLS, LTRCEMLS, BURNAREA, PROBFIRE)
C    B ------------------OUTPUTS ABOVE THIS LINE ----------------------
C
C               CANADIAN TERRESTRIAL ECOSYSTEM MODEL (CTEM) V1.0
C                           DISTURBANCE SUBROUTINE
C
C     15  MAY 2003  - THIS SUBROUTINE CALCULATES THE LITTER GENERATED
C     V. ARORA        AND C EMISSIONS FROM LEAVES, STEM, AND ROOT 
C                     COMPONENTS DUE TO FIRE. C EMISSIONS FROM BURNED
C                     LITTER ARE ALSO ESTIMATED. AT PRESENT NO OTHER 
C                     FORM OF DISTURBANCE IS MODELLED.
C     INPUTS 
C
C     STEMMASS  - STEM MASS FOR EACH OF THE 9 CTEM PFTs, Kg C/M2
C     ROOTMASS  - ROOT MASS FOR EACH OF THE 9 CTEM PFTs, Kg C/M2
C     GLEAFMAS  - GREEN LEAF MASS FOR EACH OF THE 9 CTEM PFTs, Kg C/M2
C     BLEAFMAS  - BROWN LEAF MASS 
C     THLIQ     - LIQUID SOIL MOISTURE CONTENT
C     WILTSM    - WILTING POINT SOIL MOISTURE CONTENT
C     FIELDSM   - FIELD CAPACITY SOIL MOISTURE CONTENT
C     UWIND     - WIND SPEED, M/S
C     VWIND     - WIND SPEED, M/S
C     LIGHTNG   - TOTAL LIGHTNING, FLASHES/(KM^2.YEAR)
C                 IT IS ASSUMED THAT CLOUD TO GROUND LIGHTNING IS
C                 SOME FIXED FRACTION OF TOTAL LIGHTNING.
C     FCANCMX   - FRACTIONAL COVERAGES OF CTEM's 9 PFTs
C     LITRMASS  - LITTER MASS FOR EACH OF THE 9 PFTs
C     PRBFRHUC  - PROBABILITY OF FIRE DUE TO HUMAN CAUSES
C     RMATCTEM  - FRACTION OF ROOTS IN EACH SOIL LAYER FOR EACH PFT
C     EXTNPROB  - FIRE EXTINGUISHING PROBABILITY
C     PFTAREAB  - AREAS OF DIFFERENT PFTs IN A GRID CELL, BEFORE FIRE, KM^2
C     ICC       - NO. OF CTEM PLANT FUNCTION TYPES, CURRENTLY 9
C     IG        - NO. OF SOIL LAYERS (CURRENTLY 3)
C     ILG       - NO. OF GRID CELLS IN LATITUDE CIRCLE
C     IL1,IL2   - IL1=1, IL2=ILG
C     SORT      - INDEX FOR CORRESPONDENCE BETWEEN 9 PFTs AND SIZE 12 OF
C                 PARAMETERS VECTORS
C     NOL2PFTS  - NUMBER OF LEVEL 2 CTEM PFTs
C     IC        - NUMBER OF CLASS PFTs
C     GRCLAREA  - GCM GRID CELL AREA, KM^2
C     THICE     - FROZEN SOIL MOISTURE CONTENT OVER CANOPY FRACTION
C
C     OUTPUTS
C
C     STEMLTDT  - STEM LITTER GENERATED DUE TO DISTURBANCE (Kg C/M2)
C     ROOTLTDT  - ROOT LITTER GENERATED DUE TO DISTURBANCE (Kg C/M2)
C     GLFLTRDT  - GREEN LEAF LITTER GENERATED DUE TO DISTURBANCE (Kg C/M2)
C     BLFLTRDT  - BROWN LEAF LITTER GENERATED DUE TO DISTURBANCE (Kg C/M2)
C     BURNAREA  - TOTAL AREA BURNED, KM^2
C     PROBFIRE  - PROBABILITY OF FIRE
C     PFTAREAA  - AREAS OF DIFFERENT PFTs IN A GRID CELL, AFTER FIRE, KM^2
C
C     NOTE THE FOLLOWING C EMISSIONS CAN BE EASILY CONVERTED INTO CO2
C     EMISSIONS BY MULTIPLYING WITH A CONSTANT.
C
C     GLCAEMLS  - GREEN LEAF CARBON EMISSION LOSSES, Kg C/M2
C     BLCAEMLS  - BROWN LEAF CARBON EMISSION LOSSES, Kg C/M2
C     RTCAEMLS  - ROOT CARBON EMISSION LOSSES, Kg C/M2
C     STCAEMLS  - STEM CARBON EMISSION LOSSES, Kg C/M2
C     LTRCEMLS  - LITTER CARBON EMISSION LOSSES, Kg C/M2
C
      IMPLICIT NONE
C
      INTEGER    ILG,     ICC,      IL1,        IL2,      I,      J,    
     1             K,      IG,    ISEED,  FIRE(ILG),     KK,      M,
     2            K1,      K2,        N,         IC
C
      PARAMETER (KK=12)  ! PRODUCT OF CLASS PFTs AND L2MAX
C
      INTEGER       SORT(ICC),      NOL2PFTS(IC)
C
      REAL  STEMMASS(ILG,ICC), ROOTMASS(ILG,ICC), GLEAFMAS(ILG,ICC),
     1      BLEAFMAS(ILG,ICC),     THLIQ(ILG,IG),    WILTSM(ILG,IG),
     2        FIELDSM(ILG,IG),        UWIND(ILG),        VWIND(ILG),
     3       FCANCMX(ILG,ICC),      LIGHTNG(ILG),LITRMASS(ILG,ICC+1),
     4          PRBFRHUC(ILG),     EXTNPROB(ILG), PFTAREAB(ILG,ICC),
     5   RMATCTEM(ILG,ICC,IG),     THICE(ILG,IG)
C
      REAL  STEMLTDT(ILG,ICC), ROOTLTDT(ILG,ICC), GLFLTRDT(ILG,ICC),
     1          BURNAREA(ILG), PFTAREAA(ILG,ICC), GLCAEMLS(ILG,ICC),
     2      RTCAEMLS(ILG,ICC), STCAEMLS(ILG,ICC), LTRCEMLS(ILG,ICC),
     3      BLFLTRDT(ILG,ICC), BLCAEMLS(ILG,ICC)
C
      REAL        BMASTHRS(2),                PI,             ZERO,
     1               EXTNMOIS,          LWRLTHRS,         HGRLTHRS,
     2               PARMLGHT,          PARBLGHT,            ALPHA,
     3                     F0,           MAXSPRD,      FRCO2LF(KK),
     4            FRLTRLF(KK),      FRCO2STM(KK),     FRLTRSTM(KK),
     5            FRCO2RT(KK),       FRLTRRT(KK),     FRLTRBRN(KK)
C
      REAL   BIOMASS(ILG,ICC),        BTERM(ILG), DRGTSTRS(ILG,ICC),
     1       BETADRGT(ILG,IG),     AVGDRYNS(ILG),        FCSUM(ILG),
     2          AVGBMASS(ILG),        MTERM(ILG),     C2GLGTNG(ILG),
     3          BETALGHT(ILG),            Y(ILG),              YMIN,
     4                   YMAX,             SLOPE,        LTERM(ILG),
     5          PROBFIRE(ILG),             CTIME,            RANDOM,
     6                   TEMP,     BETMSPRD(ILG),       SMFUNC(ILG),
     7              WIND(ILG),      WNDFUNC(ILG),     SPRDRATE(ILG),
     8           LBRATIO(ILG),     ARBN1DAY(ILG),     AREAMULT(ILG),
     9       BURNVEG(ILG,ICC),      VEGAREA(ILG),     GRCLAREA(ILG),
     A                REPAREA
C
      REAL   RAND
C     ------------------------------------------------------------------
C                     CONSTANTS USED IN THE MODEL
C     NOTE THE STRUCTURE OF VECTORS WHICH CLEARLY SHOWS THE CLASS
C     PFTs (ALONG ROWS) AND CTEM SUB-PFTs (ALONG COLUMNS)
C
C     NEEDLE LEAF |  EVG       DCD       ---
C     BROAD LEAF  |  EVG   DCD-CLD   DCD-DRY
C     CROPS       |   C3        C4       ---
C     GRASSES     |   C3        C4       ---
C
C     MIN. AND MAX. VEGETATION BIOMASS THRESHOLDS TO INITIATE FIRE, KG C/M^2
      DATA BMASTHRS/0.20, 1.0/
C
      DATA PI/3.141592654/
C
C     EXTINCTION MOISTURE CONTENT FOR ESTIMATING FIRE LIKELINESS DUE
C     TO SOIL MOISTURE
      DATA EXTNMOIS/0.30/
C
C     LOWER CLOUD-TO-GROUND LIGHTNING THRESHOLD FOR FIRE LIKELIHOOD
C     FLASHES/KM^2.YEAR
      DATA LWRLTHRS/0.25/
C
C     HIGHER CLOUD-TO-GROUND LIGHTNING THRESHOLD FOR FIRE LIKELIHOOD
C     FLASHES/KM^2.YEAR
      DATA HGRLTHRS/10.0/
C
C     PARAMETER M (MEAN) AND B OF LOGISTIC DISTRIBUTION USED FOR 
C     ESTIMATING FIRE LIKELIHOOD DUE TO LIGHTNING
      DATA PARMLGHT/0.4/
      DATA PARBLGHT/0.1/
C
C     PARAMETER ALPHA AND F0 USED FOR ESTIMATING WIND FUNCTION FOR
C     FIRE SPREAD RATE
      DATA ALPHA/8.16326E-04/
      DATA F0/0.1/
C
C     MAX. FIRE SPREAD RATE, KM/HR
      DATA MAXSPRD/0.45/
C
C     FRACTION OF LEAF BIOMASS CONVERTED TO CO2 DUE TO COMBUSTION
      DATA FRCO2LF/0.70, 0.70, 0.00,
     &             0.70, 0.70, 0.70,
     &             0.00, 0.00, 0.00,
     &             0.80, 0.80, 0.00/
C
C     FRACTION OF LEAF BIOMASS BECOMING LITTER AFTER COMBUSTION
      DATA FRLTRLF/0.20, 0.20, 0.00,
     &             0.20, 0.20, 0.20,
     &             0.00, 0.00, 0.00,
     &             0.10, 0.10, 0.00/
C
C     FRACTION OF STEM BIOMASS CONVERTED TO CO2 DUE TO COMBUSTION
      DATA FRCO2STM/0.20, 0.20, 0.00,
     &              0.20, 0.10, 0.10,
     &              0.00, 0.00, 0.00,
     &              0.00, 0.00, 0.00/
C
C     FRACTION OF STEM BIOMASS BECOMING LITTER AFTER COMBUSTION
      DATA FRLTRSTM/0.60, 0.60, 0.00,
     &              0.60, 0.40, 0.40,
     &              0.00, 0.00, 0.00,
     &              0.00, 0.00, 0.00/
C
C     FRACTION OF ROOT BIOMASS CONVERTED TO CO2 DUE TO COMBUSTION
      DATA FRCO2RT/0.0, 0.0, 0.0,
     &             0.0, 0.0, 0.0, 
     &             0.0, 0.0, 0.0, 
     &             0.0, 0.0, 0.0/
C
C     FRACTION OF ROOT BIOMASS BECOMING LITTER AFTER COMBUSTION
      DATA FRLTRRT/0.10, 0.10, 0.00,
     &             0.10, 0.10, 0.10,
     &             0.00, 0.00, 0.00,
     &             0.25, 0.25, 0.00/
C
C     FRACTION OF LITTER BURNED DURING FIRE AND EMITTED AS CO2
      DATA FRLTRBRN/0.50, 0.50, 0.00,
     &              0.60, 0.60, 0.60,
     &              0.00, 0.00, 0.00,
     &              0.70, 0.70, 0.00/
C
C     TYPICAL AREA REPRESENTING CTEM's FIRE PARAMETERIZATION
      DATA REPAREA/1000.0/ ! KM^2
C
C     ZERO
      DATA ZERO/1E-20/
C
C     ---------------------------------------------------------------
C
      IF(ICC.NE.9)                            CALL XIT('DISTURB',-1)
C
C     INITIALIZE REQUIRED ARRAYS TO ZERO
C
      DO 140 J = 1,ICC
        DO 150 I = IL1, IL2
          STEMLTDT(I,J)=0.0     !STEM LITTER DUE TO DISTURBANCE
          ROOTLTDT(I,J)=0.0     !ROOT LITTER DUE TO DISTURBANCE
          GLFLTRDT(I,J)=0.0     !GREEN LEAF LITTER DUE TO DISTURBANCE
          BLFLTRDT(I,J)=0.0     !BROWN LEAF LITTER DUE TO DISTURBANCE
          BIOMASS(I,J)=0.0      !TOTAL BIOMASS FOR FIRE PURPOSES
          DRGTSTRS(I,J)=0.0     !SOIL DRYNESS FACTOR FOR PFTs
          BURNVEG(I,J)=0.0      !BURN AREA FOR EACH PFT
          PFTAREAA(I,J)=0.0     !PFT AREA AFTER FIRE
          GLCAEMLS(I,J)=0.0     !GREEN LEAF CARBON FIRE EMISSIONS
          BLCAEMLS(I,J)=0.0     !BROWN LEAF CARBON FIRE EMISSIONS
          STCAEMLS(I,J)=0.0     !STEM CARBON FIRE EMISSIONS
          RTCAEMLS(I,J)=0.0     !ROOT CARBON FIRE EMISSIONS
          LTRCEMLS(I,J)=0.0     !LITTER CARBON FIRE EMISSIONS
150     CONTINUE                  
140   CONTINUE
C
      DO 160 K = 1,IG
        DO 170 I = IL1, IL2
          BETADRGT(I,K)=0.0     !DRYNESS TERM FOR SOIL LAYERS
170     CONTINUE                  
160   CONTINUE
C
      DO 180 I = IL1, IL2
        AVGBMASS(I)=0.0         !AVG. VEG. BIOMASS OVER THE VEG. FRACTION 
C                               !OF GRID CELL
        AVGDRYNS(I)=0.0         !AVG. DRYNESS OVER THE VEGETATED FRACTION
        FCSUM(I)=0.0            !TOTAL VEGETATED FRACTION
        BTERM(I)=0.0            !BIOMASS FIRE PROBABILITY TERM
        MTERM(I)=0.0            !MOISTURE FIRE PROBABILITY TERM
        C2GLGTNG(I)=0.0         !CLOUD-TO-GROUND LIGHTNING
        BETALGHT(I)=0.0         !0-1 LIGHTNING TERM
        Y(I)=0.0                !LOGISTIC DIST. FOR FIRE PROB. DUE TO LIGHTNING
        LTERM(I)=0.0            !LIGHTNING FIRE PROBABILITY TERM
        PROBFIRE(I)=0.0         !PROBABILITY OF FIRE
        FIRE(I)=0               !FIRE, 1 MEANS YES, 0 MEANS NO
        BURNAREA(I)=0.0         !TOTAL AREA BURNED DUE TO FIRE
        BETMSPRD(I)=0.0         !BETA MOISTURE FOR CALCULATING FIRE SPREAD RATE
        SMFUNC(I)=0.0           !SOIL MOISTURE FUNCTION USED FOR FIRE SPREAD RATE
        WIND(I)=0.0             !WIND SPEED IN KM/HR
        WNDFUNC(I)=0.0          !WIND FUNCTION FOR FIRE SPREAD RATE
        SPRDRATE(I)=0.0         !FIRE SPREAD RATE
        LBRATIO(I)=0.0          !LENGTH TO BREADTH RATIO OF FIRE
        ARBN1DAY(I)=0.0         !AREA BURNED IN 1 DAY, KM^2
        AREAMULT(I)=0.0         !MULTIPLIER TO FIND AREA BURNED
        VEGAREA(I)=0.0          !TOTAL VEGETATED AREA IN A GRID CELL
180   CONTINUE
C
      DO 190 I = IL1, IL2
        IF(EXTNPROB(I).LE.ZERO) THEN
          WRITE(6,*)'FIRE EXTINGUISHING PROB. (',I,'= ',EXTNPROB(I)
          WRITE(6,*)'PLEASE USE AN APPROPRIATE VALUE OF THIS PARAMATER'
          WRITE(6,*)'ELSE THE WHOLE GRID CELL WILL BURN DOWN LEADING TO'
          WRITE(6,*)'NUMERICAL PROBLEMS.'
          CALL XIT('DISTURB',-2)
        ENDIF
190   CONTINUE
C
C     INITIALIZATION ENDS    
C
C     ------------------------------------------------------------------
C
C     FIND THE PROBABILITY OF FIRE AS A PRODUCT OF THREE FUNCTIONS
C     WITH DEPENDENCE ON TOTAL BIOMASS, SOIL MOISTURE, AND LIGHTNING 
C
C     1. DEPENDENCE ON TOTAL BIOMASS
C
      DO 200 J = 1, ICC
        DO 210 I = IL1, IL2
C         ROOT BIOMASS IS NOT USED TO INITIATE FIRE. FOR EXAMPLE IF
C         THE LAST FIRE BURNED ALL GRASS LEAVES, AND SOME OF THE ROOTS
C         WERE LEFT, ITS UNLIKELY THESE ROOTS COULD CATCH FIRE. 
          BIOMASS(I,J)=GLEAFMAS(I,J)+BLEAFMAS(I,J)+STEMMASS(I,J)+
     &                 LITRMASS(I,J)
210     CONTINUE
200   CONTINUE
C
C     FIND AVERAGE BIOMASS OVER THE VEGETATED FRACTION
C
      DO 220 J = 1, ICC
        DO 230 I = IL1, IL2
          AVGBMASS(I) = AVGBMASS(I)+BIOMASS(I,J)*FCANCMX(I,J)
230     CONTINUE
220   CONTINUE 
C
      DO 250 I = IL1, IL2
        FCSUM(I)=FCANCMX(I,1)+FCANCMX(I,2)+FCANCMX(I,3)+FCANCMX(I,4)+  
     &           FCANCMX(I,5)+FCANCMX(I,6)+FCANCMX(I,7)+FCANCMX(I,8)+
     &           FCANCMX(I,9)

        IF(FCSUM(I).GT.ZERO)THEN
          AVGBMASS(I)=AVGBMASS(I)/FCSUM(I)
        ELSE
          AVGBMASS(I)=0.0
        ENDIF
C
        IF(AVGBMASS(I).GE.BMASTHRS(2))THEN
          BTERM(I)=1.0
        ELSE IF(AVGBMASS(I).LT.BMASTHRS(2).AND.
     &    AVGBMASS(I).GT.BMASTHRS(1))THEN
          BTERM(I)=(AVGBMASS(I)-BMASTHRS(1))/(BMASTHRS(2)-BMASTHRS(1))     
        ELSE IF(AVGBMASS(I).LE.BMASTHRS(1))THEN
          BTERM(I)=0.0  !NO FIRE IF BIOMASS BELOW THE LOWER THRESHOLD
        ENDIF
        BTERM(I)=MAX(0.0, MIN(BTERM(I),1.0))
250   CONTINUE 
C
C     2. DEPENDENCE ON SOIL MOISTURE
C
C     THIS IS CALCULATED IN A WAY SUCH THAT MORE DRY THE ROOT ZONE
C     OF A PFT TYPE IS, AND MORE FRACTIONAL AREA IS COVERED WITH THAT
C     PFT, THE MORE LIKELY IT IS THAT FIRE WILL GET STARTED. THAT IS
C     THE DRYNESS FACTOR IS WEIGHTED BY FRACTION OF ROOTS IN SOIL
C     LAYERS, AS WELL AS ACCORDING TO THE FRACTIONAL COVERAGE OF 
C     DIFFERENT PFTs. THE ASSUMPTION HERE IS THAT IF THERE IS LESS 
C     MOISTURE IN ROOT ZONE, THEN IT IS MORE LIKELY THE VEGETATION 
C     WILL BE DRY AND THUS THE LIKELINESS OF FIRE IS MORE.
C
C     FIRST FIND THE DRYNESS FACTOR FOR EACH SOIL LAYER.
C
      DO 300 J = 1, IG
        DO 310 I = IL1, IL2
C
          IF((THLIQ(I,J)+THICE(I,J)).LE.WILTSM(I,J)) THEN
            BETADRGT(I,J)=0.0
          ELSE IF((THLIQ(I,J)+THICE(I,J)).GT.WILTSM(I,J).AND.
     &      (THLIQ(I,J)+THICE(I,J)).LT.FIELDSM(I,J))THEN
            BETADRGT(I,J)=(THLIQ(I,J)+THICE(I,J)-WILTSM(I,J))
            BETADRGT(I,J)=BETADRGT(I,J)/(FIELDSM(I,J)-WILTSM(I,J))   
          ELSE
            BETADRGT(I,J)=1.0
          ENDIF
          BETADRGT(I,J)=MAX(0.0, MIN(BETADRGT(I,J),1.0))
C
310     CONTINUE
300   CONTINUE
C
C     NOW FIND WEIGHTED VALUE OF THIS DRYNESS FACTOR AVERAGED OVER 
C     THE ROOTING DEPTH, FOR EACH PFT
C
      DO 320 J = 1, ICC
        DO 330 I = IL1, IL2
         DRGTSTRS(I,J) =  (BETADRGT(I,1))*RMATCTEM(I,J,1) +
     &                    (BETADRGT(I,2))*RMATCTEM(I,J,2) +
     &                    (BETADRGT(I,3))*RMATCTEM(I,J,3)
         DRGTSTRS(I,J) = DRGTSTRS(I,J) /
     &    (RMATCTEM(I,J,1)+RMATCTEM(I,J,2)+RMATCTEM(I,J,3))
         DRGTSTRS(I,J)=MAX(0.0, MIN(DRGTSTRS(I,J),1.0))
330     CONTINUE
320   CONTINUE
C
C     NEXT FIND THIS DRYNESS FACTOR AVERAGED OVER THE VEGETATED FRACTION
C
      DO 350 J = 1, ICC
        DO 360 I = IL1, IL2
          AVGDRYNS(I) = AVGDRYNS(I)+DRGTSTRS(I,J)*FCANCMX(I,J)
360     CONTINUE
350   CONTINUE 
C
      DO 370 I = IL1, IL2
        IF(FCSUM(I).GT.ZERO)THEN
          AVGDRYNS(I)=AVGDRYNS(I)/FCSUM(I)
        ELSE
          AVGDRYNS(I)=0.0
        ENDIF
370   CONTINUE 
C
C     USE AVERAGE ROOT ZONE VEGETATION DRYNESS TO FIND LIKELIHOOD OF
C     FIRE DUE TO MOISTURE. 
C
      DO 380 I = IL1, IL2
        IF(FCSUM(I).GT.ZERO)THEN
          MTERM(I)=EXP( -1.0*PI*(AVGDRYNS(I)/EXTNMOIS)**2 )
        ELSE
          MTERM(I)=0.0   !NO FIRE LIKELIHOOD DUE TO MOISTURE IF NO VEGETATION
        ENDIF
        MTERM(I)=MAX(0.0, MIN(MTERM(I),1.0))
380   CONTINUE
C
C     3. DEPENDENCE ON LIGHTNING
C
C     DEPENDENCE ON LIGHTNING IS MODELLED IN A SIMPLE WAY WHICH IMPLIES THAT
C     A LARGE NO. OF LIGHTNING FLASHES ARE MORE LIKELY TO CAUSE FIRE THAN
C     FEW LIGHTNING FLASHES.
C
      DO 400 I = IL1, IL2
        C2GLGTNG(I)=0.25*LIGHTNG(I)   
        IF( C2GLGTNG(I).LE.LWRLTHRS) THEN
           BETALGHT(I)=0.0
        ELSE IF ( C2GLGTNG(I).GT.LWRLTHRS.AND.
     &  C2GLGTNG(I).LT.HGRLTHRS) THEN 
           BETALGHT(I)=(C2GLGTNG(I)-LWRLTHRS)/(HGRLTHRS-LWRLTHRS)
        ELSE IF (C2GLGTNG(I).GE.HGRLTHRS) THEN
           BETALGHT(I)=1.0
        ENDIF
        Y(I)=1.0/( 1.0+EXP((PARMLGHT-BETALGHT(I))/PARBLGHT) )
        YMIN=1.0/( 1.0+EXP((PARMLGHT-0.0)/PARBLGHT) )
        YMAX=1.0/( 1.0+EXP((PARMLGHT-1.0)/PARBLGHT) )
        SLOPE=ABS(0.0-YMIN)+ABS(1.0-YMAX)
        TEMP=Y(I)+(0.0-YMIN)+BETALGHT(I)*SLOPE
        LTERM(I)=TEMP+(1.0-TEMP)*PRBFRHUC(I)
        LTERM(I)=MAX(0.0, MIN(LTERM(I),1.0))
400   CONTINUE
C
C     MULTIPLY THE BTERM, MTERM, AND THE LTERM TO FIND PROBABILITY OF
C     FIRE. ALSO GENERATE A RANDOM NUMBER TO SEE IF WE ARE GOING TO
C     START A FIRE OR NOT.
C
      CALL TIME(CTIME)        !Returns the current time as set within the system
      ISEED=100000000*CTIME
      CALL SRAND (ISEED)
      DO 420 I = IL1, IL2
        PROBFIRE(I)=BTERM(I)*MTERM(I)*LTERM(I)
        RANDOM = RAND()     
        IF(RANDOM.LT.PROBFIRE(I))THEN
          FIRE(I)=1
        ENDIF
420   CONTINUE
C
C     IF FIRE IS TO BE STARTED THEN ESTIMATE BURN AREA AND LITTER GENERATED
C     BY THE FIRE, ELSE DO NOTHING.

      DO 430 I = IL1, IL2
        IF(FIRE(I).EQ.1)THEN
C
C         FIND SPREAD RATE AS A FUNCTION OF WIND SPEED AND SOIL MOISTURE IN THE
C         ROOT ZONE (AS FOUND ABOVE) WHICH WE USE AS A SURROGATE FOR MOISTURE
C         CONTENT OF VEGETATION.
          IF( AVGDRYNS(I).GT.EXTNMOIS )THEN
            BETMSPRD(I)= 1.0   
          ELSE
            BETMSPRD(I)= AVGDRYNS(I)/EXTNMOIS   
          ENDIF
          SMFUNC(I)=(1.0-BETMSPRD(I))**2.0
          WIND(I)=SQRT(UWIND(I)**2.0 + VWIND(I)**2.0)
          WIND(I)=WIND(I)*3.60     ! CHANGE M/S TO KM/HR
          WNDFUNC(I)=1.0 - ( (1.0-F0)*EXP(-1.0*ALPHA*WIND(I)**2) )
          SPRDRATE(I)=MAXSPRD* SMFUNC(I)* WNDFUNC(I)
C
C         LENGTH TO BREADTH RATIO OF FIRE
          LBRATIO(I)=1.0+10.0*(1.0-EXP(-0.017*WIND(I)))
C
C         AREA BURNED IN 1 DAY, KM^2
          ARBN1DAY(I)=(PI*0.36*24*24*SPRDRATE(I)**2)/LBRATIO(I)
C
C         BASED ON FIRE EXTINGUISHING PROBABILITY WE ESTIMATE THE NUMBER 
C         WHICH NEEDS TO BE MULTIPLIED WITH ARBN1DAY TO ESTIMATE AVERAGE 
C         AREA BURNED
          AREAMULT(I)=((1.0-EXTNPROB(I))*(2.0-EXTNPROB(I)))/
     &      EXTNPROB(I)**2.0                   
C
C         AREA BURNED, KM^2
          BURNAREA(I)=ARBN1DAY(I)*AREAMULT(I)
          BURNAREA(I)=BURNAREA(I)*(GRCLAREA(I)/REPAREA)*PROBFIRE(I)
        ENDIF
430   CONTINUE
C
C     MAKE SURE AREA BURNED IS NOT GREATER THAN THE VEGETATED AREA. 
C     DISTRIBUTE BURNED AREA EQUALLY AMONGST PFTs PRESENT IN THE GRID CELL.
C 
      DO 460 I = IL1, IL2
        VEGAREA(I)=PFTAREAB(I,1)+PFTAREAB(I,2)+PFTAREAB(I,3)+
     &             PFTAREAB(I,4)+PFTAREAB(I,5)+PFTAREAB(I,6)+
     &             PFTAREAB(I,7)+PFTAREAB(I,8)+PFTAREAB(I,9)  
        IF(BURNAREA(I).GT.VEGAREA(I)) THEN
          BURNAREA(I)=VEGAREA(I)
        ENDIF
460   CONTINUE
C
      K1=0
      DO 470 J = 1, IC
       IF(J.EQ.1) THEN
         K1 = K1 + 1
       ELSE
         K1 = K1 + NOL2PFTS(J-1)
       ENDIF
       K2 = K1 + NOL2PFTS(J) - 1
       DO 475 M = K1, K2
        DO 480 I = IL1, IL2
          IF(VEGAREA(I).GT.ZERO)THEN
            BURNVEG(I,M)= (BURNAREA(I)*PFTAREAB(I,M)/VEGAREA(I))
            IF(J.EQ.3)THEN  !CROPS NOT ALLOWED TO BURN
              BURNVEG(I,M)= 0.0
            ENDIF
          ELSE
            BURNVEG(I,M)= 0.0
          ENDIF
          PFTAREAA(I,M)=PFTAREAB(I,M)-BURNVEG(I,M)
480     CONTINUE
475    CONTINUE 
470   CONTINUE 
C
      DO 490 I = IL1, IL2
       BURNAREA(I)=BURNVEG(I,1)+BURNVEG(I,2)+BURNVEG(I,3)+BURNVEG(I,4)+  
     &             BURNVEG(I,5)+BURNVEG(I,6)+BURNVEG(I,7)+BURNVEG(I,8)+  
     &             BURNVEG(I,9)
490   CONTINUE
C
C     CHECK THAT THE SUM OF FRACTION OF LEAVES, STEM, AND ROOT 
C     THAT NEEDS TO BE BURNED AND CONVERTED INTO CO2, AND FRACTION THAT 
C     NEEDS TO BECOME LITTER DOESN'T EXCEED ONE.
C
      DO 500 J = 1, ICC
        N = SORT(J)
        IF( (FRCO2LF(N)+FRLTRLF(N)).GT.1.0 )    CALL XIT('DISTURB',-3)
        IF( (FRCO2STM(N)+FRLTRSTM(N)).GT.1.0 )  CALL XIT('DISTURB',-4)
        IF( (FRCO2RT(N)+FRLTRRT(N)).GT.1.0 )    CALL XIT('DISTURB',-5)
        IF(  FRLTRBRN(N).GT.1.0 )               CALL XIT('DISTURB',-6)
500   CONTINUE
C
C     AND FINALLY ESTIMATE AMOUNT OF LITTER GENERATED FROM EACH PFT, AND
C     EACH VEGETATION COMPONENT (LEAVES, STEM, AND ROOT) BASED ON THEIR
C     RESISTANCE TO COMBUSTION. WE ALSO ESTIMATE CO2 EMISSIONS FROM EACH
C     OF THESE COMPONENTS. NOTE THAT THE LITTER WHICH IS GENERATED DUE 
C     TO DISTURBANCE IS UNIFORMLY DISTRIBUTED OVER THE ENTIRE AREA OF 
C     A GIVEN PFT, AND THIS ESSENTIALLY THINS THE VEGETATION BIOMASS. 
C     AT THIS STAGE WE DO NOT MAKE THE BURN AREA BARE, AND THEREFORE
C     FIRE DOESN'T CHANGE THE FRACTIONAL COVERAGE OF PFTs.  
C
      DO 520 J = 1, ICC
        N = SORT(J)
        DO 530 I = IL1, IL2
C
          IF(PFTAREAB(I,J).GT.ZERO)THEN
            GLFLTRDT(I,J)=FRLTRLF(N) *GLEAFMAS(I,J)*
     &       (BURNVEG(I,J)/PFTAREAB(I,J))
C
            BLFLTRDT(I,J)=FRLTRLF(N) *BLEAFMAS(I,J)*
     &       (BURNVEG(I,J)/PFTAREAB(I,J))
C
            STEMLTDT(I,J)=FRLTRSTM(N)*STEMMASS(I,J)*
     &       (BURNVEG(I,J)/PFTAREAB(I,J))
C
            ROOTLTDT(I,J)=FRLTRRT(N) *ROOTMASS(I,J)*
     &       (BURNVEG(I,J)/PFTAREAB(I,J))
C
            GLCAEMLS(I,J)=FRCO2LF(N) *GLEAFMAS(I,J)*
     &       (BURNVEG(I,J)/PFTAREAB(I,J))
C
            BLCAEMLS(I,J)=FRCO2LF(N) *BLEAFMAS(I,J)*
     &       (BURNVEG(I,J)/PFTAREAB(I,J))
C
            STCAEMLS(I,J)=FRCO2STM(N)*STEMMASS(I,J)*
     &       (BURNVEG(I,J)/PFTAREAB(I,J))
C
            RTCAEMLS(I,J)=FRCO2RT(N) *ROOTMASS(I,J)*
     &       (BURNVEG(I,J)/PFTAREAB(I,J))
C
            LTRCEMLS(I,J)=FRLTRBRN(N)*LITRMASS(I,J)*
     &       (BURNVEG(I,J)/PFTAREAB(I,J))
          ELSE
            GLFLTRDT(I,J)=0.0
            BLFLTRDT(I,J)=0.0
            STEMLTDT(I,J)=0.0
            ROOTLTDT(I,J)=0.0
            GLCAEMLS(I,J)=0.0
            BLCAEMLS(I,J)=0.0
            STCAEMLS(I,J)=0.0
            RTCAEMLS(I,J)=0.0
            LTRCEMLS(I,J)=0.0
          ENDIF
C
530     CONTINUE
520   CONTINUE
C
      RETURN
      END

